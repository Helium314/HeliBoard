apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdk 33

    defaultConfig {
        applicationId "org.dslul.openboard.inputmethod.latin"
        minSdkVersion 19
        targetSdkVersion 33
        versionCode 19
        versionName '1.4.5'
    }

    buildTypes {
        release {
            minifyEnabled true
            //shrinkResources true todo: test before enabling, a lot of resources are accessed via getIdentifier and possibly not all have been marked with "keep"
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'src/main/proguard.flags'
        }
        debug {
            minifyEnabled true
            jniDebuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'src/main/proguard.flags'
            applicationIdSuffix ".debug"
        }
        archivesBaseName = "openboard_" + defaultConfig.versionName
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    lintOptions {
        abortOnError false
    }

    ndkVersion '25.2.9519653'
    androidResources { // todo: check, probably both can be removed (together with searching dicts in res/raw)
        noCompress 'main.dict'
        noCompress 'empty.dict'
    }

    packagingOptions {
        jniLibs {
            // shrinks APK by 3 MB, zipped size unchanged
            useLegacyPackaging true
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    namespace "org.dslul.openboard.inputmethod.latin"
}

dependencies {
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.preference:preference:1.2.1' // includes appcompat
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'com.github.martin-stone:hsv-alpha-color-picker-android:3.1.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.12.4'
    testImplementation 'org.mockito:mockito-inline:3.12.4'
    testImplementation 'org.robolectric:robolectric:4.9'
    testImplementation 'androidx.test:runner:1.5.2'
    testImplementation 'androidx.test:core:1.5.0'
}

repositories {
    maven { url "https://jitpack.io" }
}
